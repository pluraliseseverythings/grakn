allprojects  {
  apply plugin: 'maven'

  tasks.withType(Test)  {
    maxParallelForks = 1
    testLogging {
      // set options for log level LIFECYCLE
      events "passed", "skipped", "failed", "standardOut"
      showExceptions true
      exceptionFormat "full"
      showCauses true
      showStackTraces true

      // set options for log level DEBUG and INFO
      debug {
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
      }
      info.events = debug.events
      info.exceptionFormat = debug.exceptionFormat

      afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
          def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
          def startItem = '|  ', endItem = '  |'
          def repeatLength = startItem.length() + output.length() + endItem.length()
          println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
        }
      }
    }
  }

  group = 'ai.grakn'
version = '0.17.0-SNAPSHOT'
}

subprojects {
  apply plugin: 'java'
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
  tasks.withType(JavaCompile) {
  	options.encoding = 'UTF-8'
  }

  task packageSources(type: Jar) {
classifier = 'sources'
from sourceSets.main.allSource

}
artifacts.archives packageSources
  repositories {
    mavenLocal()
    
    maven { url "https://maven.grakn.ai/content/repositories/snapshots/" }
    maven { url "http://repo.maven.apache.org/maven2" }
  }

  
  dependencies {
    compile group: 'com.google.guava', name: 'guava', version:'19.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.20'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version:'2.6.4'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    testCompile group: 'ch.qos.logback', name: 'logback-core', version:'1.1.7'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.7'
    testCompile group: 'org.codehaus.janino', name: 'janino', version:'2.7.8'
    testCompile group: 'net.bytebuddy', name: 'byte-buddy', version:'1.6.14'
    testCompile group: 'net.bytebuddy', name: 'byte-buddy-agent', version:'1.6.14'
    compileOnly group: 'com.google.auto.value', name: 'auto-value', version:'1.4.1'
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version:'3.0.1'
}

  
}

